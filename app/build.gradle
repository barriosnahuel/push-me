apply plugin:'com.android.application'
apply plugin:'com.getkeepsafe.dexcount'
apply plugin:'com.jakewharton.hugo'
apply plugin:'com.monits.staticCodeAnalysis'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 26
    buildToolsVersion '26.0.1'

    defaultConfig {
        applicationId "com.github.barriosnahuel.vossosunboton"
        minSdkVersion 18
        targetSdkVersion 26
        versionCode 4
        versionName '1.1.0'
    }

    signingConfigs {
        release {

            File keystoreFile = file('../nahuelbarrios.keystore')
            File propertiesFile = file("../secure.properties")
            if (keystoreFile.exists() && propertiesFile.exists()) {
                logger.warn('#### INFO: Release keystore will be used ####')
                Properties properties = new Properties()
                properties.load(new FileInputStream(propertiesFile))

                keyAlias properties.getProperty('key.alias')
                keyPassword properties.getProperty('key.password')
                storeFile keystoreFile
                storePassword properties.getProperty('store.password')
            } else {
                logger.warn('#### WARNING: Neither release keystore nor the secure properties were found. Debug keystore will be used ####')
                keyAlias "androiddebugkey"
                keyPassword "android"
                storeFile file("../debug_keystore.keystore")
                storePassword "android"
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

staticCodeAnalysis {
    findbugs = false
    checkstyle = false // Disabled because it throws: "* What went wrong: Could not determine the dependencies of task ':app:checkstyleDebug'.  > Resolving configuration 'debugApk' directly is not allowed"
    pmd = true
    cpd = true
    androidLint = true

    ignoreErrors = false

    androidLintConfig = "$project.rootProject.projectDir/config/android/android-lint-app.xml"
}

dependencies {
    def supportLibrariesVersion = '26.0.1'
    implementation "com.android.support:support-v4:$supportLibrariesVersion"
    implementation "com.android.support:appcompat-v7:$supportLibrariesVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibrariesVersion"

    implementation 'com.jakewharton.timber:timber:4.5.1'

    //androidLint 'com.monits:android-linters:1.3.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.1'

    def stethoVersion = '1.5.0'
    debugImplementation("com.facebook.stetho:stetho:$stethoVersion") {
        // Excluded because we couldn't apply dex-count plugin. ClassNotFoundException can be thrown when using Stetho
        exclude group:'com.google.code.findbugs', module:'jsr305'
    }

    debugImplementation 'com.nshmura:strictmode-notifier:0.9.3'

    testImplementation 'junit:junit:4.12'
}
