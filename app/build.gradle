apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.monits.staticCodeAnalysis'
apply plugin: 'io.gitlab.arturbosch.detekt'
//apply plugin: 'jacoco-android'
apply from: rootProject.file('jacoco.gradle')

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.barriosnahuel.vossosunboton"
        minSdkVersion 19
        targetSdkVersion 29
        versionCode 4
        versionName '2.0.0'
    }

    signingConfigs {
        release {

            File keystoreFile = file('../nahuelbarrios.keystore')
            File propertiesFile = file("../secure.properties")
            if (keystoreFile.exists() && propertiesFile.exists()) {
                logger.warn('WARNING: Release keystore will be used for this build.')
                Properties properties = new Properties()
                properties.load(new FileInputStream(propertiesFile))

                keyAlias properties.getProperty('key.alias')
                keyPassword properties.getProperty('key.password')
                storeFile keystoreFile
                storePassword properties.getProperty('store.password')
            } else {
                logger.warn('WARNING: Neither release keystore nor the secure properties were found. Debug keystore will be used for this build.')
                keyAlias "androiddebugkey"
                keyPassword "android"
                storeFile file("../debug_keystore.keystore")
                storePassword "android"
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true

            all {
                testLogging {
                    events 'PASSED', 'FAILED'
                }

                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG ${new Date().format("YYYY-MM-dd HH:mm:ss")}"
            multiDexEnabled true
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

//jacoco {
//    toolVersion = '0.8.5'
//}
//
//jacocoAndroidUnitTestReport {
//    xml.enabled = false
//    html.enabled = true
//}

staticCodeAnalysis {
    findbugs = false
    checkstyle = true
    pmd = true
    cpd = true
    androidLint = true

    ignoreErrors = false

    androidLintConfig = "$project.rootProject.projectDir/config/android/android-lint-app.xml"
}

detekt {
    config = files("../config/detekt/detekt-config.yml")
}

dependencies {
    implementation project(':commons_file')
    implementation project(':commons_android')
    implementation project(':feature_base')
    implementation project(':feature_addbutton')
    implementation project(':model')

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'

    implementation 'com.google.firebase:firebase-core:17.2.2'
    implementation 'com.google.firebase:firebase-perf:19.0.5'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    debugImplementation 'androidx.multidex:multidex:2.0.1'

    // Skipped because it's a major change and I have to make changes to use v2.x. Task added to Trello.
    // noinspection NewerVersionAvailable
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    testImplementation 'junit:junit:4.13'

    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'

    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'io.mockk:mockk:1.9.3'
}

apply plugin: 'com.google.gms.google-services'