apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'com.getkeepsafe.dexcount'
apply from: "$project.rootProject.projectDir/linters.gradle"

android {
    namespace 'com.github.barriosnahuel.vossosunboton'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
    }

    compileSdkVersion 32

    defaultConfig {
        applicationId "com.github.barriosnahuel.vossosunboton"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 5
        versionName '2.0.0'
    }

    resourcePrefix 'app_'

    signingConfigs {
        release {

            File keystoreFile = file("$project.rootProject.projectDir/nahuelbarrios.keystore-appbundle.pkcs12")
            File propertiesFile = file("$project.rootProject.projectDir/secure.properties")
            if (keystoreFile.exists() && propertiesFile.exists()) {
                logger.warn('WARNING: Release keystore is available for app signing')
                Properties properties = new Properties()
                properties.load(new FileInputStream(propertiesFile))

                keyAlias properties.getProperty('key.alias')
                keyPassword properties.getProperty('key.password')
                storeFile keystoreFile
                storePassword properties.getProperty('store.password')
            } else {
                keyAlias "androiddebugkey"
                keyPassword "android"
                storeFile file("$project.rootProject.projectDir/debug_keystore.keystore")
                storePassword "android"
            }
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources = true

            all {
                testLogging {
                    events 'PASSED', 'FAILED'
                }
            }
        }
    }

    buildTypes {
        debug {
            // Comment this suffix to use a debug build at Internal App Sharing: https://play.google.com/apps/publish/internalappsharing/
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG ${new Date().format("YYYY-MM-dd HH:mm:ss")}"
            multiDexEnabled true
        }

        release {
            minifyEnabled true
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    dynamicFeatures = [":feature_addbutton"]

}

dependencies {
    implementation project(':commons_file')
    implementation project(':commons_android')
    implementation project(':model')

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.1'

    implementation platform ('com.google.firebase:firebase-bom:30.4.1')

    implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-perf'

    implementation 'com.jakewharton.timber:timber:5.0.1'

    debugImplementation 'com.facebook.flipper:flipper:0.164.0'
    debugImplementation 'com.facebook.soloader:soloader:0.10.4'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'
    debugImplementation 'com.facebook.flipper:flipper-leakcanary2-plugin:0.164.0'

    testImplementation 'junit:junit:4.13.2'

    testImplementation 'androidx.test:core:1.4.0'
    testImplementation 'org.robolectric:robolectric:4.8.2'

    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'io.mockk:mockk:1.12.7'
}