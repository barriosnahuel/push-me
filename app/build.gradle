apply plugin:'com.android.application'
apply plugin:'com.getkeepsafe.dexcount'
apply plugin:'com.jakewharton.hugo'
apply plugin:'com.monits.staticCodeAnalysis'

android {
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.barriosnahuel.vossosunboton"
        minSdkVersion 18
        targetSdkVersion 28
        versionCode 4
        versionName '2.0.0'
    }

    signingConfigs {
        release {

            File keystoreFile = file('../nahuelbarrios.keystore')
            File propertiesFile = file("../secure.properties")
            if (keystoreFile.exists() && propertiesFile.exists()) {
                logger.warn('#### INFO: Release keystore will be used ####')
                Properties properties = new Properties()
                properties.load(new FileInputStream(propertiesFile))

                keyAlias properties.getProperty('key.alias')
                keyPassword properties.getProperty('key.password')
                storeFile keystoreFile
                storePassword properties.getProperty('store.password')
            } else {
                logger.warn('#### WARNING: Neither release keystore nor the secure properties were found. Debug keystore will be used ####')
                keyAlias "androiddebugkey"
                keyPassword "android"
                storeFile file("../debug_keystore.keystore")
                storePassword "android"
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

staticCodeAnalysis {
    findbugs = false
    checkstyle = true
    pmd = true
    cpd = true
    androidLint = true

    ignoreErrors = false

    androidLintConfig = "$project.rootProject.projectDir/config/android/android-lint-app.xml"
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    implementation 'com.google.firebase:firebase-core:17.0.0'

    implementation 'com.jakewharton.timber:timber:4.7.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.3'

    debugImplementation 'com.facebook.stetho:stetho:1.5.1'

    debugImplementation 'com.nshmura:strictmode-notifier:0.9.3'

    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'