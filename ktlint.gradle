/**
 * See https://github.com/pinterest/ktlint#usage for more
 */

configurations {
    ktlint
}

dependencies {
    ktlint 'com.pinterest:ktlint:0.33.0'
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "src/**/*.kt",
            "--reporter=plain",
            "--reporter=json,output=${buildDir}/reports/ktlint/ktlint.json"
}

check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "formatting") {
    description = "Fix Kotlin code style deviations."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args "-F", "src/**/*.kt"
}